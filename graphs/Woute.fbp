EXPORT=SERVER.LISTEN:LISTEN
EXPORT=SPLITROUTES.IN:ROUTES
EXPORT=MERGEROUTES.IN:ROUTE
EXPORT=ROUTER.OUT:OUT
EXPORT=RESPONDER.IN:IN

',' -> DELIMITER SplitRoutes(SplitStr)
'^(.*)$' -> PATTERN AddCaret(Replace)
'^$1' -> REPLACEMENT AddCaret()
SplitRoutes() OUT -> IN MergeRoutes(Merge) OUT -> IN AddCaret() OUT -> ROUTE Router(routers/RegexpRouter)

Server(webserver/Server) REQUEST -> IN ParseBody(webserver/BodyParser) OUT -> IN Receiver(woute/Receiver)
Receiver() URL -> IN UrlSplit(Split)

MergeSections(packets/MergeConnections)

'session-id' -> GROUP SessionId(Group)
Receiver() SID -> IN SessionId() OUT -> IN MergeSections()
'url' -> GROUP Url(Group)
UrlSplit() OUT -> IN Url() OUT -> IN MergeSections()
'headers' -> GROUP Headers(Group)
Receiver() HEADERS -> IN Headers() OUT -> IN MergeSections()
'body' -> GROUP Body(Group)
Receiver() BODY -> IN Body() OUT -> IN MergeSections()
'response' -> GROUP Response(Group)
Receiver() RESPONSE -> IN Response() OUT -> IN MergeSections()

UrlSplit() OUT -> IN StringifyUrl(woute/StringifyUrl) OUT -> GROUP GroupConnection(groups/Group)
MergeSections() OUT -> IN GroupConnection() OUT -> IN Router()

Responder(woute/Responder)
