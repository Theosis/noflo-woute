EXPORT=REQUEST.IN:IN
EXPORT=SESSIONID.OUT:SID
EXPORT=EXTRACTRESPONSE.OUT:RESPONSE
EXPORT=REQUESTOBJECT.OUT:REQUEST
EXPORT=URL.OUT:URL
EXPORT=EXTRACTBODY.OUT:BODY
EXPORT=EXTRACTHEADERS.OUT:HEADERS

Request(Split) OUT -> IN SessionId(swiss/RandomUuid)
'^[a-f0-9]{8}-[a-f0-9]{4}-[a-f0-9]{4}-[a-f0-9]{4}-[a-f0-9]{12}$' -> REGEXP RemoveSessionId(groups/Remove)
Request() OUT -> IN RemoveSessionId() OUT -> IN FirstInRequest(packets/First) OUT -> IN RequestResponseObject(Split)

'req' -> KEY ExtractRequest(objects/ExtractProperty)
RequestResponseObject() OUT -> IN ExtractRequest() OUT -> IN RequestObject(Split)
'res' -> KEY ExtractResponse(objects/ExtractProperty)
RequestResponseObject() OUT -> IN ExtractResponse()

'url' -> KEY ExtractUrl(objects/ExtractProperty)
'/' -> DELIMITER SplitUrl(SplitStr)
RequestObject() OUT -> IN ExtractUrl() OUT -> IN SplitUrl() OUT -> IN DecodeUri(woute/DecodeUri) OUT -> IN Url(swiss/Compact)

'body' -> KEY ExtractBody(objects/ExtractProperty)
RequestObject() OUT -> IN ExtractBody()

'headers' -> KEY ExtractHeaders(objects/ExtractProperty)
RequestObject() OUT -> IN ExtractHeaders()
